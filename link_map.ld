ENTRY(eventHandlerShim)
GROUP(libgcc.a libc.a libm.a)

/* Forces a particular cache-line alignment, for stability
	(reduce impact of "performance lottery")
	
	The particular values used were just ones that seem to
	give good performance, but further tinkering may be desirable.
	
	. = ALIGN(32);
	. += (OFFSET);
*/

SECTIONS
{
	.text :
	{        	
		__text_start__ = .;
		. = ALIGN(32);
		*(.text.main)
		
		. = ALIGN(32);
		*(.text.tick)
		
		. = ALIGN(32);
		*(.text.pgb)

		. = ALIGN(32);
		*(.text)
		*(.text.*)
		
		/* code to copy to TCM, seemingly runs faster */
		. = ALIGN(32);
		. += 0xc;
        __itcm_start = .;
		*(.itcm.short)
		. = ALIGN(32);
		. += 0x6;
	    *(.itcm)
	    *(.itcm.*)
        __itcm_end = .;
		
		. = ALIGN(32);
		__audio_start__ = .;
		*(.audio)
		*(.audio.*)
		
		. = ALIGN(32);
		__rare_start__ = .;
		*(.rare)
		*(.rare.*)
        
		KEEP(*(.init))
		KEEP(*(.fini))

		/* .ctors */
		*crtbegin.o(.ctors)
		*crtbegin?.o(.ctors)
		*(EXCLUDE_FILE(*crtend?.o *crtend.o) .ctors)
		*(SORT(.ctors.*))
		*(.ctors)

		/* .dtors */
 		*crtbegin.o(.dtors)
 		*crtbegin?.o(.dtors)
 		*(EXCLUDE_FILE(*crtend?.o *crtend.o) .dtors)
 		*(SORT(.dtors.*))
 		*(.dtors)
		
		. = ALIGN(32);
		
		__rodata_start__ = .;
		*(.rodata*)

		KEEP(*(.eh_frame*))
	}

	.data :
	{
		__etext = .;
		
		. = ALIGN(32);

		__data_start__ = .;
		*(vtable)
		*(.data*)

		. = ALIGN(4);
		/* preinit data */
		PROVIDE_HIDDEN (__preinit_array_start = .);
		KEEP(*(.preinit_array))
		PROVIDE_HIDDEN (__preinit_array_end = .);

		. = ALIGN(4);
		/* init data */
		PROVIDE_HIDDEN (__init_array_start = .);
		KEEP(*(SORT(.init_array.*)))
		KEEP(*(.init_array))
		PROVIDE_HIDDEN (__init_array_end = .);

		. = ALIGN(4);
		/* finit data */
		PROVIDE_HIDDEN (__fini_array_start = .);
		KEEP(*(SORT(.fini_array.*)))
		KEEP(*(.fini_array))
		PROVIDE_HIDDEN (__fini_array_end = .);

		. = ALIGN(4);
		/* All data end */
		__data_end__ = .;

	}

	.bss :
	{
		. = ALIGN(32);
		__bss_start__ = .;
		*(.bss*)
		*(COMMON)
		*(COM)
		. = ALIGN(4);
		__bss_end__ = .;
	}

  /DISCARD/ :
  {
		*(.ARM.exidx)
  }

}
